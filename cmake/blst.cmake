include(ExternalProject)

set(BLST_SOURCE_DIR "${PROJECT_SOURCE_DIR}/third_party/blst")
set(BLST_CC "${CMAKE_C_COMPILER} ${CMAKE_C_FLAGS}")

ExternalProject_Add(
    blst
    SOURCE_DIR "${BLST_SOURCE_DIR}"
    CONFIGURE_COMMAND ""
    BUILD_COMMAND ./build.sh CC='${BLST_CC}' AR='${CMAKE_AR}'
    BUILD_IN_SOURCE TRUE
    BUILD_BYPRODUCTS "<SOURCE_DIR>/${CMAKE_STATIC_LIBRARY_PREFIX}blst${CMAKE_STATIC_LIBRARY_SUFFIX}"
    LOG_BUILD TRUE
    LOG_OUTPUT_ON_FAILURE TRUE
    INSTALL_COMMAND ""
)

add_library(blst::blst STATIC IMPORTED GLOBAL)
set_target_properties(blst::blst PROPERTIES
    INTERFACE_INCLUDE_DIRECTORIES ${BLST_SOURCE_DIR}/bindings
    IMPORTED_LOCATION ${BLST_SOURCE_DIR}/${CMAKE_STATIC_LIBRARY_PREFIX}blst${CMAKE_STATIC_LIBRARY_SUFFIX}
)
